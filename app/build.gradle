apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// ###
def GLOBALGIVING_API_KEY = '"' + GlobalGivingApiKey + '"' ?: '"Define Global Giving API Key"'
// ###

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "org.gratitude"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Please ensure you have a valid API KEY for globalgiving to use this app
        // A valid key will need to be entered
        buildConfigField 'String', 'GlobalGivingApiKey', GLOBALGIVING_API_KEY

        resConfigs "en"
    }

    signingConfigs {
        config
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.config
        }
    }

    dataBinding.enabled = true
}

def Properties props = new Properties()
def propFile = new File('gradle.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.config.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.config.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.config.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.config.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'gradle.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'gradle.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:design:$rootProject.supportLibVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibVersion"
    implementation "com.android.support:customtabs:$rootProject.supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Intro
    implementation 'com.github.apl-devs:appintro:v4.2.3'
    // Expandable TextView
    implementation 'com.ms-square:expandableTextView:0.1.4'
    // Attribution
    implementation 'com.github.franmontiel:AttributionPresenter:1.0'
    // Room
    implementation "android.arch.persistence.room:runtime:$rootProject.roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.roomVersion"
    androidTestImplementation "android.arch.persistence.room:testing:$rootProject.roomVersion"
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$rootProject.lifecycle_version"
    annotationProcessor "android.arch.lifecycle:compiler:$rootProject.lifecycle_version"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    // CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    // PhotoView
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.3'
    // FirebaseUI for Firebase Auth
    implementation 'com.firebaseui:firebase-ui-auth:4.1.0'
    // Firebase/Google Sign-in
    implementation 'com.google.firebase:firebase-auth:16.0.3'
    implementation 'com.google.android.gms:play-services-auth:16.0.0'
    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'
    // Billing
    implementation 'com.android.billingclient:billing:1.1'
    // Test
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.orhanobut:logger:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply plugin: 'com.google.gms.google-services'